# Used to build an index for a genome fasta
rule bowtie2_build:
    threads: 16
    conda: '../envs/bowtie2_env.yml'
    log: 'logs/bowtie2_build.log'

    input:
        config['genome_fasta']

    output:
        touch('logs/bowtie2_build.done')

    params:
        index_name      = config['bowtie2']['index_name'],
        index_folder    = directory(config['bowtie2']['index_folder'])

    shell:
        '''
        mkdir -p {params.index_folder}
        bowtie2-build {input} {params.index_folder}/{params.index_name} \
                -p {threads} \
                > {log} 2>&1
        '''


# Used to align sequencing reads to a genome fasta using the built index (previous rule)
# NOTE: bowtie2 produces sam, use samtools to convert to bam automatically
rule bowtie2_align:
    threads: 24
    conda: '../envs/bowtie2_env.yml'
    log: 'logs/bowtie2_align_{sample}.log'

    input:
       r1p = 'results/raw_reads_trimmed/{sample}_r1p.fastq.gz',
       r2p = 'results/raw_reads_trimmed/{sample}_r2p.fastq.gz',
       index_check      = 'logs/bowtie2_build.done'

    output:
        bam             = 'results/bowtie2_alignments/{sample}.bam'

    params:
        index_path      = config['bowtie2']['index_path'],
        max_frag_length = config['bowtie2']['max_frag_length']

    shell:
        '''
        bowtie2 -p {threads} \
                -x {params.index_path} \
                -1 {input.r1p} \
                -2 {input.r2p} \
                -X {params.max_frag_length} 2> {log} | samtools view -bS - > {output.bam}
        '''
